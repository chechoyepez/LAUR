*========================================================================================================================
    SUBROUTINE PACS.TRIGGER.ACTT
*========================================================================================================================
* This routines can be used to process the existing AAA (INPUT, AUTHORISE, REVERSE and DELETE).
* This can be used to trigger dummy activity like (CHANGE-SCHEDULE, UPDATE-OFFICERS etc)
*
! ACTIVITY.NAME#EFFECTIVE.DATE#ARRANGEMENT.ID#AAA.ID#FUNC#TYPE
!
! AAA.ID and FUNC can be used while amend/delete/reverse/auth
!
! Create version AA.ARRANGEMENT.ACTIVITY,TEST
! Create OFS.SOURECE - AA.CORR as same as AA.COB
!
*========================================================================================================================
** Modification history
*-----------------------
** 14 Apr 2016 - Pandiarajan - Logic added to remove the id after processing the OFS
*
*
*========================================================================================================================
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.AA.ARRANGEMENT
*========================================================================================================================
*
    GOSUB INIT
    GOSUB PROCESS
    RETURN
*========================================================================================================================
INIT:

    COMO.DATE=TODAY
    COMO.TIME=TIME()
    LIST.NAME = "SL.ACT1"

    FN.AA = 'F.AA.ARRANGEMENT'
    FV.AA = ''
    CALL OPF(FN.AA,FV.AA)

    FN.COMO='&COMO&'
    F.COMO=''
    CALL OPF(FN.COMO,F.COMO)

    FN.SL = "&SAVEDLISTS&"
    F.SL = ""
    CALL OPF(FN.SL, F.SL)

    FAILED = ''; PROCESSED = ''

    READ RREC FROM F.SL, LIST.NAME ELSE NULL
	*RREC = 'LENDING-CHANGE-SCHEDULE#20210127#AA19019QFJP4##I#'
    RETURN

*========================================================================================================================
PROCESS:

	
    TEMP.REC = RREC

    LOOP
        REMOVE AA.ID FROM RREC SETTING AA.POS
    WHILE AA.ID : AA.POS

        TEMP.ERR = ""
        Y.DATE = FIELD(AA.ID,'#',2)
        Y.ACTIVITY = FIELD(AA.ID,'#',1)
        AAA.ID = FIELD(AA.ID,'#',4)
        FUNC = FIELD(AA.ID,'#',5)
        Y.TYPE = FIELD(AA.ID,'#',6)
        AA.ID = FIELD(AA.ID,'#',3)

        AA.ID = TRIM(AA.ID)
        R.AA = ''; AA.ERR = ''
        CALL F.READ(FN.AA,AA.ID,R.AA,FV.AA,AA.ERR)

        IF NOT(AA.ERR) THEN

            IF Y.DATE OR AAA.ID THEN

                SAVE.ID.COMPANY = ID.COMPANY
                CALL LOAD.COMPANY(R.AA<AA.ARR.CO.CODE>)

                GOSUB FORM.OFS.MESSAGE
                GOSUB PROCESS.OFS
                GOSUB UPDATE.SL

                CALL LOAD.COMPANY(SAVE.ID.COMPANY)
            END ELSE
                TEMP.ERR = "Date is not provided"
                GOSUB UPDATE.SL
            END
        END ELSE
            TEMP.ERR = "Arrangement record missing : ":AA.ID
            GOSUB UPDATE.SL
        END

    REPEAT

    RETURN
*========================================================================================================================
FORM.OFS.MESSAGE:

    IF AAA.ID THEN
        options<1> = "AA.CORR"
        OFS.RECORD = "AA.ARRANGEMENT.ACTIVITY,TEST/":FUNC:"/PROCESS///,//":ID.COMPANY:",":AAA.ID
    END ELSE
        IF NOT(Y.TYPE) THEN
            Y.TYPE = "USER"
        END

        options<1> = "AA.CORR"
        options<4> = "HLD"
        OFS.RECORD = "AA.ARRANGEMENT.ACTIVITY,TEST/I/PROCESS///,//":ID.COMPANY:",,ARRANGEMENT=":AA.ID:",EFFECTIVE.DATE=":Y.DATE:",ACTIVITY=":Y.ACTIVITY:",INITIATION.TYPE=":Y.TYPE
    END

    RETURN
*========================================================================================================================
PROCESS.OFS:

    theResponse = ""
    txnCommitted = ""

    CALL OFS.CALL.BULK.MANAGER(options, OFS.RECORD, theResponse, txnCommitted)
    RETURN
*========================================================================================================================
UPDATE.SL:

    DEL TEMP.REC<1>
    CALL F.WRITE(FN.SL,LIST.NAME,TEMP.REC)

    IF TEMP.ERR THEN
        FAILED<-1> = AA.ID:"#":TEMP.ERR
        TEMP.ERR=0
    END ELSE
        PROCESSED <-1>= AA.ID
    END

    PROCESS.ID = LIST.NAME:"_PROCESSED_":COMO.DATE:"_":COMO.TIME
    FAILED.ID = LIST.NAME:"_FAILED_"::COMO.DATE:"_":COMO.TIME
    RESP.ID = LIST.NAME:"_RESPONSE_":COMO.DATE:"_":COMO.TIME

    RESP = theResponse

    CALL F.WRITE(FN.COMO,PROCESS.ID,PROCESSED)
    CALL F.WRITE(FN.COMO,FAILED.ID,FAILED)

    CALL F.WRITE(FN.SL,PROCESS.ID,PROCESSED)
    CALL F.WRITE(FN.SL,FAILED.ID,FAILED)
    CALL F.WRITE(FN.SL,RESP.ID,RESP)
    
    CALL JOURNAL.UPDATE('')

    RETURN
*========================================================================================================================
